-- @path qu=/at.ac.tuwien.big.questionnaire2form/metamodels/questionnaire.ecore
-- @path fo=/at.ac.tuwien.big.questionnaire2form/metamodels/form.ecore

module questionnaire2form;
create OUT: fo from IN: qu;

helper def : id : Integer = 0;
helper context Integer def: inc() : Integer = self + 1;

rule Questionnaire2Form {
	from q: qu!Questionnaire
	to f: fo!Form (
		pages <- q.questionGroups,
		welcomePage <- q.questionGroups.first()
	)
}

rule QuestionGroup2Page {
	from qg: qu!QuestionGroup
	to p: fo!Page (
		title <- qg.name,		
		elements <- h1,
		elements <- h2,
		elements <- qg.questions,
		allNextPages <- p.refImmediateComposite().pages ->
						select(q | q.refImmediateComposite().pages -> indexOf(q)  >
								   q.refImmediateComposite().pages -> indexOf(p)),
		allPreviousPages <- p.refImmediateComposite().pages ->
						select(q | q.refImmediateComposite().pages -> indexOf(q)  <
								   q.refImmediateComposite().pages -> indexOf(p)),
								   
		previousPage <- p.allPreviousPages.last(),
		nextPage <- p.allNextPages.first()
	),
	
	h1 : fo!Heading(
		level <- 1,
		content <- qg.refImmediateComposite().title,
		page <- p
	),

	h2 : fo!Heading(
		level <- 2,
		content <- qg.name,
		page <- p
	)
	
	do {
		thisModule.id <- thisModule.id.inc();
		h1.elementId <- thisModule.id.toString();
		thisModule.id <- thisModule.id.inc();
		h2.elementId <- thisModule.id.toString();
		
		for (a in qg.questions->select(q | q.oclIsTypeOf(qu!ClosedEndedQuestion))->collect(cq | cq.answers)->flatten()) {
			-- Open Answers -> Textfields
			if (a.openAnswer) {
				p.elements <- thisModule.Answer2_Open_Textfield(a);
			}
		}
	}
}

-- OPEN QUESTION

rule OpenEndedQuestion_SL_2TextField {
	from o: qu!OpenEndedQuestion (not o.multiline)
	to tf: fo!TextField(
		label <- o.text,
		mandatory <- o.mandatory
	)
	do {
		thisModule.id <- thisModule.id.inc();
		tf.elementId <- thisModule.id.toString();
	}
}

rule OpenEndedQuestion_ML_2TextArea {
	from o: qu!OpenEndedQuestion (o.multiline)
	to ta: fo!TextArea(
		label <- o.text,
		mandatory <- o.mandatory
	)
	do {
		thisModule.id <- thisModule.id.inc();
		ta.elementId <- thisModule.id.toString();
	}
}

-- CLOSED QUESTION

abstract rule ClosedEndedQuestion2SelectionField {
	from c: qu!ClosedEndedQuestion (c.answers.size() > 1)
	to sf: fo!SelectionField (
		label <- c.text,
		items <- c.answers,
		mandatory <- c.mandatory
	)
}

rule ClosedEndedQuestion_MA_2SelectionField extends ClosedEndedQuestion2SelectionField {
	from c: qu!ClosedEndedQuestion (c.type = #MultiAnswer)
	to sf: fo!SelectionField (
		selectionFieldType <- #Checkbox
	)
	do {
		thisModule.id <- thisModule.id.inc();
		sf.elementId <- thisModule.id.toString();
	}
}

rule ClosedEndedQuestion_SA_2SelectionField extends ClosedEndedQuestion2SelectionField {
	from c: qu!ClosedEndedQuestion (c.type = #SingleAnswer)
	to sf: fo!SelectionField (
		selectionFieldType <- #Radio
	)
	do {
		thisModule.id <- thisModule.id.inc();
		sf.elementId <- thisModule.id.toString();
	}
}

-- LikertScaleQuestion

rule LikertScaleQuestion2SelectionField {
	from l: qu!LikertScaleQuestion
	to sf: fo!SelectionField (
		selectionFieldType <- #Combobox,
		label <- l.text,
		mandatory <- l.mandatory
	)
	do {
		thisModule.makeLikertOption(l.lowerBound, l.upperBound, sf);
		thisModule.id <- thisModule.id.inc();
		sf.elementId <- thisModule.id.toString();
	}
}

rule makeLikertOption(current: Integer, end: Integer, sf: fo!SelectionField) {
	to si: fo!SelectionItem (
		label <- current.toString()
	)
	do {
		sf.items <- si;
		if (current < end) {
			thisModule.makeLikertOption(current + 1, end, sf);
		}
	}
}

-- ANSWERS

rule Answer2_Closed_SelectionItem {
	from a: qu!Answer (a.enablesQuestion.size() = 0)
	to si: fo!SelectionItem (
		label <- a.text,
		selected <- a.refImmediateComposite().defaultAnswer = a	
	)
}

rule Answer2_Closed_SelectionItem_and_Cond {
	from a: qu!Answer (a.enablesQuestion.size() > 0)
	to si: fo!SelectionItem (
		label <- a.text,
		selected <- a.refImmediateComposite().defaultAnswer = a
	),
	sc: fo!SelectionCondition (
		item <- si,
		concernsElements <- a.enablesQuestion,
		page <- a.refImmediateComposite().refImmediateComposite()
	)
}

lazy rule Answer2_Open_Textfield {
	from a: qu!Answer
	to si: fo!TextField (
		label <- a.text,
		mandatory <- a.refImmediateComposite().mandatory
	)
	do {
		thisModule.id <- thisModule.id.inc();
		si.elementId <- thisModule.id.toString();
	}
}



